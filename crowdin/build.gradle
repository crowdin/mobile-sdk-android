plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version "2.2.0"
}

apply from: '../jacoco.gradle'

ext {
    // Use centralized version from gradle.properties
    libraryVersionCode = project.property('libraryVersionCode') as Integer
    libraryVersionName = project.property('crowdinVersion')

    publishedGroupId = project.property('publishedGroupId')
    libraryName = 'CrowdinAndroidSdk'
    artifact = 'sdk'

    libraryDescription = "Crowdin Android SDK delivers all new translations from Crowdin project to the application immediately. So there is no need to update this application via Google Play Store to get the new version with the localization."

    siteUrl = project.property('siteUrl')
    gitUrl = project.property('gitUrl')

    libraryVersion = libraryVersionName

    developerId = project.property('developerId')
    developerName = project.property('developerName')
    developerEmail = project.property('developerEmail')

    licenseName = project.property('licenseName')
    licenseUrl = project.property('licenseUrl')
    allLicenses = ["MIT"]
}

android {
    namespace 'com.crowdin.platform'
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode libraryVersionCode
        versionName libraryVersionName
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        buildConfigField "int", "MIN_COMPOSE_API_LEVEL", "24"
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.11.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.12.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.24"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'com.karumi:dexter:6.2.2'
    implementation "androidx.multidex:multidex:2.0.1"

    // Jetpack Compose dependencies
    implementation platform(libs.androidx.compose.bom)
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-text'
    implementation 'androidx.compose.runtime:runtime'
    implementation 'androidx.compose.foundation:foundation'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.1'
}

// KtLint configuration
configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest.ktlint:ktlint-cli:1.4.0")
}

tasks.register("ktlint", JavaExec) {
    group = "verification"
    description = "Check Kotlin code style with ktlint."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args = ["format", "src/**/*.kt"] // use 'check' instead of 'format' for lint check only
}

check.dependsOn "ktlint"

tasks.register("ktlintFormat", JavaExec) {
    group = "formatting"
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main") // use `mainClass.set()` instead of `main`
    args = ["-F", "src/**/*.kt"] // `-F` enables formatting
}

apply from: 'maven.gradle'
