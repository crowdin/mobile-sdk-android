apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = libraryVersion


if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact("$buildDir/outputs/aar/crowdin-release.aar")
            groupId
            artifactId artifact
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

// Bintray
bintray {
    user = property('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = property('bintray.key') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Publication']
    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        userOrg = 'crowdin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/crowdin/mobile-sdk-android'
        override = true

        version {
            desc = libraryDescription
            gpg {
                sign = true
            }
        }
    }
}